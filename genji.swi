/**
源氏香の全解、または、集合を分割するプログラム

2015_01_12

@suharahiromichi  
*/

/**  
# はじめに
  
有限の集合を分割（グループ化）できる総数をベル数という。
サイズ3の集合なら、以下の5種類の分割があるので、3のベル数 B3 = 5 となる。
    {a}, {b}, {c}
    {a}, {b, c}
    {b}, {a, c}
    {c}, {a, b}
    {a ,b, c}

源氏香は、香りを5回嗅ぐ（聞く）ことから、その答えは B5 = 52 から選ばれることになる。

ただし、実際にしらべてもベル数を計算する方法は、いくらでも見つかるが、
実際にその分割の方法を求める方法は見つけられなかったので、自分で考えてみた。

これは、アルゴリズムの問題というよりも、大げさにいうなら、
分割された集合をどのように表現するかのデータ構造の問題だから、PrologやCoqのような、
データ構造を表現＋制約（証明）で与えられる言語で解くのがよさそうである。
*/

/**
# 解き方

分割前の集合のサイズをl、1≦lとする。その要素を1からlまでの番号で呼ぶ。
要素nが、x番めのグループに属することを、

    Gn = x

と書く。グループは1からの番号を「欠番なく」付与する。
要素1は1番めのグループとする。

    G1 = 1 

すると、グループの番号に欠番を生じないためには、
n番めの要素の属するグループには以下の制約があることになる。

    1 ≦ Gn ≦ max(i = 1, n-1)Gi + 1

ここで、max(i = 1, n-1)Gi は、要素1からn-1が属する最大のグループ番号を意味する。

つまり、n番めの要素は、既存のグループに含まれるか、さもなければ、
あらたなグループ(+ 1)になるか、どちらかということになる。
  */


/**
# プログラム

まず、手軽にPrologで実装するとして、
前節でしめした制約を満たしているかいなかをチェックする処理を作成する。gchk。
  
## 制約チェック

### 要素ひとつ分ん制約のチェック
  
N番めの要素が、制約を満たすかをチェックする。gchk1。

N番めの要素がどのグループに属するかをリストLで表す。
Lが与えられたとき、LのN番め, 1≦nが、Gnとなる。nth1。

要素1からn-1までの最大のグループ数 max(i = 1, n-1)Gi は、
Lの1番めからN-1番めを取り出してその最大数をもとめる。gmax。
  
ただし、最初の要素1の場合は、グループ番号は1とする。gchk1の最初の節。
*/
gchk1(1, [1|_]) :- !.                       % G1 = 1
gchk1(N, L) :-                              % 1から数えてN番め。
        nth1(N, L, X),                      % そのグループ番号
        gmax(N, L, Max),                    % N-1の最大のグループ番号、N>1。
        Max1 is Max + 1,                    % +1したものとを、
%       writeln([X, Max1]),
        X =< Max1.                          % ここがチェック。

gmax(N, L, Max) :-
        N1 is N - 1,
        head(N1, L, R),
        max_list(R, Max).

/**
### すべての要素の対する制約のチェック

gchk1 を すべての要素に対して実行し、
すべての要素に対して終了したら、成功終了とする。
*/
gchk(L) :-
        gchk(1, L).
gchk(N, L) :-
        length(L, Len),
        N > Len.                            % 成功終了
gchk(N, L) :-
        gchk1(N, L),                        % N番めの要素をチェックする。
        N1 is N + 1,
        gchk(N1, L).


/**
### ハウスキーピング
*/

%% 先頭からN要素, N ≧ 1。
head(0, L, []).
head(N, [X|L], [X|R]) :-
        N1 is N - 1,
        head(N1, L, R).

nth1(N, L, X) :-
        N1 is N - 1,
        nth0(N1, L, X).

/**
## テストプログラム

制約チェックが正しいかを確認するために、B5 (源氏香）の全解をもとめてみる。

長さNの自然数の組み合わせのリスト、N=5なら、[1,1,1,1,1]から[5,5,5,5,5]をもとめて、
制約を満たしていたら印刷出力する。その数を数える。 

[1,1,1,1,1]から[1,2,3,4,5]まででよいのだけれど、手抜きと念入とで。
*/

/**
### 長さNの自然数の組み合わせのリスト

未定変数リスト[X,Y,Z,U,V]に対して、値をユニファイして返す。
*/
gen(L) :-
        length(L, Len), !,
        maplist(gen1(Len, 0), L).

gen1(N, M, N2) :-
        N > M,
        N1 is M + 1,
        (N2 = N1;
         gen1(N, N1, N2)).
  
/**
### 解の数を数える

副作用のあるコード。
*/  
:- dynamic(count/1).
count(0).
countup(C1) :-
        count(C),
        C1 is C + 1,
        retract(count(_)),
        assert(count(C1)).

/**
### 全体のドライバ
*/  
go :-
        L = [X,Y,Z,U,V], gen(L),
        gchk(L),
        countup(C), writeln([C, L]),
        fail.

/**
# おまけ
*/
/*
thema([1,1,1,1,1], 手習).
thema([1,1,1,1,2], 末摘花).
thema([1,1,1,2,1], 梅枝).
thema([1,1,1,2,2], 空蝉).
thema([1,1,1,2,3], ).
thema([1,1,2,1,1], ).
thema([1,1,2,1,2], ).
thema([1,1,2,1,3], ).
thema([1,1,2,2,1], ).
thema([1,1,2,2,2], ).
thema([1,1,2,2,3], ).
thema([1,1,2,3,1], ).
thema([1,1,2,3,2], ).
thema([1,1,2,3,3], ).
thema([1,1,2,3,4], ).
thema([1,2,1,1,1], ).
thema([1,2,1,1,2], ).
thema([1,2,1,1,3], ).
thema([1,2,1,2,1], ).
thema([1,2,1,2,2], ).
thema([1,2,1,2,3], ).
thema([1,2,1,3,1], ).
thema([1,2,1,3,2], ).
thema([1,2,1,3,3], ).
thema([1,2,1,3,4], ).
thema([1,2,2,1,1], ).
thema([1,2,2,1,2], ).
thema([1,2,2,1,3], 椎本).
thema([1,2,2,2,1], ).
thema([1,2,2,2,2], ).
thema([1,2,2,2,3], ).
thema([1,2,2,3,1], ).
thema([1,2,2,3,2], ).
thema([1,2,2,3,3], ).
thema([1,2,2,3,4], ).
thema([1,2,3,1,1], ).
thema([1,2,3,1,2], ).
thema([1,2,3,1,3], ).
thema([1,2,3,1,4], ).
thema([1,2,3,2,1], ).
thema([1,2,3,2,2], ).
thema([1,2,3,2,3], ).
thema([1,2,3,2,4], ).
thema([1,2,3,3,1], ).
thema([1,2,3,3,2], ).
thema([1,2,3,3,3], ).
thema([1,2,3,3,4], ).
thema([1,2,3,4,1], ).
thema([1,2,3,4,2], ).
thema([1,2,3,4,3], ).
thema([1,2,3,4,4], ).
thema([1,2,3,4,5], ).
*/
/* END */
/*
桐壺
帚木
空蝉
夕顔
若紫

紅葉賀
花宴
葵
賢木
花散里
須磨
明石
澪標
蓬生
関屋
絵合
松風
薄雲
朝顔
少女
玉鬘
初音
胡蝶
蛍
常夏
篝火
野分
行幸
藤袴
真木柱

藤裏葉
匂宮
紅梅
竹河
橋姫
椎本
総角
早蕨
宿木
東屋
浮舟
蜻蛉
手習
夢浮橋
帚木
空蝉
夕顔
若紫
末摘花
紅葉賀
花宴
葵
賢木
花散里
須磨
明石
澪標
蓬生
関屋
絵合
松風
薄雲
朝顔
少女
玉鬘
初音
胡蝶
蛍
常夏
篝火
野分
行幸
藤袴
真木柱
梅枝
藤裏葉
匂宮
紅梅
竹河
橋姫
椎本
総角
早蕨
宿木
東屋
浮舟
蜻蛉

夢浮橋
*/
